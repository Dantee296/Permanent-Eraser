#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

# Modify product version here
define(_ver, [1.9])
define(_rel, [8])

AC_PREREQ(2.61)

AC_INIT([bcwipe], _ver-_rel, [support@jetico.com])

BCWIPE_VER=_ver
BCWIPE_REL=_rel

AC_CONFIG_SRCDIR([options.h])
AC_CONFIG_HEADER([config.h])

# Init automake program
AM_INIT_AUTOMAKE(no-dependencies)

# Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL


AC_CANONICAL_HOST

PTHREAD_LIB=pthread
case "${host_os}" in
    linux*)
	AC_DEFINE([OS_LINUX], [], [Linux])
	;;
    hpux*)
	AC_DEFINE([OS_HPUX], [], [HP-UX])
	;;
    aix*)
	AC_DEFINE([OS_AIX], [], [AIX])
	PTHREAD_LIB=pthreads
	;;
    sunos*)
	AC_DEFINE([OS_SUNOS], [], [SUNOS])
	;;
    solaris*)
	AC_DEFINE([OS_SOLARIS], [], [SOLARIS])
	;;
    irix*)
	AC_DEFINE([OS_IRIX], [], [IRIX])
	;;
    osf*)
	AC_DEFINE([OS_OSF1], [], [OSF1])
	;;
    openbsd*)
	AC_DEFINE([OS_OPENBSD], [], [OPENBSD])
	;;
    freebsd*)
	AC_DEFINE([OS_FREEBSD], [], [FREEBSD])
	;;
    netbsd*)
	AC_DEFINE([OS_NETBSD], [], [NETBSD])
	;;
    darwin*)
	AC_DEFINE([OS_DARWIN], [], [MACOSX])
	CFLAGS="$CFLAGS -mmacosx-version-min=10.3 -arch i386 -arch ppc"
	LDFLAGS="$LDFLAGS -arch i386 -arch ppc"
	;;
    *)
	AC_DEFINE([OS_UNIX], [], [GENERIC UNIX])
	;;
esac

# Checks for header files.
# AC_HEADER_DIRENT
# AC_HEADER_STDC
AC_CHECK_HEADERS([fcntl.h limits.h memory.h stddef.h stdlib.h string.h unistd.h utime.h mntent.h])
AC_CHECK_HEADERS([sys/ioctl.h sys/param.h sys/statvfs.h sys/diskio.h sys/disk.h sys/mount.h sys/mnttab.h sys/mntctl.h sys/vmount.h sys/time.h sys/xattr.h])

# Checks for typedefs, structures, and compiler characteristics.
#  AC_C_CONST
#  AC_TYPE_OFF_T
#  AC_TYPE_SIZE_T
#  AC_TYPE_SSIZE_T
#  AC_TYPE_UID_T
# AC_CHECK_MEMBERS([struct stat.st_blksize])

# Checks for library functions.
# AC_FUNC_LSTAT
# AC_FUNC_LSTAT_FOLLOWS_SLASHED_SYMLINK
# AC_FUNC_STAT
AC_CHECK_FUNCS([ftruncate memset mkdir rmdir strdup strerror strtol utime gettimeofday pread pwrite getmntent listxattr removexattr setxattr getxattr])
AC_DEFINE([_GNU_SOURCE], [], [Enable GNU extensions])
AC_DEFINE([lseek_f], [], [lseek()/lseek64() function])
AC_CHECK_FUNC([lseek64], AC_DEFINE([lseek_f], [lseek64]), AC_DEFINE([lseek_f], [lseek]))
AC_DEFINE([statvfs_f], [], [statvfs()/statvfs64() function and structure])
AC_CHECK_FUNC([statvfs64], AC_DEFINE([statvfs_f], [statvfs64]), AC_DEFINE([statvfs_f], [statvfs]))

AC_ARG_ENABLE([pthreads],
	    [AS_HELP_STRING([--enable-pthreads],
	    [enable multithreaded operation (default is no)])],
	    [enable_pthreads=$enableval],
	    [enable_pthreads=no])
	    
if test x$enable_pthreads = xyes; then
	AC_MSG_NOTICE([--- MULTI-THREADED MODE ---])
	LDFLAGS="$LDFLAGS -l$PTHREAD_LIB"
	AC_DEFINE([WIPE_THREADS], [], [Enable multithreaded operation])
	AC_CHECK_HEADERS([pthread.h])
fi

AC_CONFIG_FILES([Makefile])
AC_SUBST([BCWIPE_VER])
AC_SUBST([BCWIPE_REL])
AC_OUTPUT(bcwipe.spec)
AC_OUTPUT(osxpkg.sh)
chmod +x osxpkg.sh

